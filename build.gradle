plugins {
    id 'org.springframework.boot' version '3.4.2' apply false
    id 'com.diffplug.spotless' version '7.0.2' apply false
}

ext {
    set('springCloudVersion', "2024.0.0")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    group = 'io.github.nktltvnv'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_23
        toolchain {
            languageVersion = JavaLanguageVersion.of(23)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // Spring Cloud
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")

        implementation 'org.springframework.boot:spring-boot-starter'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    spotless {
        java {
            palantirJavaFormat()
            removeUnusedImports()
            importOrder('java', 'javax|jakarta', '', 'org.springframework', 'io.github.nktltvnv', '\\#')
        }
    }

    compileJava.dependsOn 'spotlessApply'
}
